Name      display_controller;
PartNo    1;
Date      2015-12-06;
Revision  00;
Designer  swallace;
Company   ;
Assembly  None;
Location  None;
Device    G22V10;

/*
Description
===========


PLD pin descriptions
====================

	[S0..2]		2..4	[in]	State counter inputs
	nUR			5	[in]	Host read, upper byte
	nLR			6	[in]	Host read, lower byte
	nUW			7	[in]	Host write, upper byte
	nLW			8	[in]	Host write, lower byte
	DISPEN			9	[in]	Display enable
	nCTLADDR		10	[in]	Address points to controller (not frame buffer)
	nID			11	[in]	Peripheral ID cycle in progress
	nCS			13	[in]	Peripheral chip select

	nFBUR			14	[out]	Frame buffer read, upper byte
	nFBLR			15	[out]	Frame buffer read, lower byte
	nFBUW			16	[out]	Frame buffer write, upper byte
	nFBLW			17	[out]	Frame buffer write, lower byte
	PIXL			18	[out]	Pixel data latch
	HOSTL			19	[out]	Host data latch
	PIXAEN			20	[out]	Pixel address bus buffer enable
	HOSTAEN		21	[out]	Host address bus buffer enable
	nCTLCS			22	[out]	MC6845 controller chip select

	1 input, 1 I/O left
	- deal with host DTACK somehow

Target device pinout
====================

                         22V10
                      _____ _____
             CLK/IN -| 1   U  24 |- VCC
  S0             IN -| 2      23 |- I/O    
  S1             IN -| 3      22 |- I/O	  nCTLCS
  S2             IN -| 4      21 |- I/O    HOSTAEN
  nUR            IN -| 5      20 |- I/O	  PIXAEN
  nLR            IN -| 6      19 |- I/O	  HOSTL
  nUW            IN -| 7      18 |- I/O	  PIXL
  nLW            IN -| 8      17 |- I/O	  nFBLW
  DISPEN         IN -| 9      16 |- I/O	  nFBUW
  nCTLADDR       IN -| 10     15 |- I/O	  nFBLR
  nID            IN -| 11     14 |- I/O	  nFBUR
                GND -| 12     13 |- I/nOE	  nCS
                      ----------- 
*/

/* Inputs */
Pin  [2..4] = [S0..2];
Pin  5 = !UR;
Pin  6 = !LR;
Pin  7 = !UW;
Pin  8 = !LW;
Pin  9 = DISPEN;
Pin 10 = !CTLADDR;
Pin 11 = !ID;
Pin 13 = !CS;

/** Outputs **/

Pin 14 = !FBUR;
Pin 15 = !FBLR;
Pin 16 = !FBUW;
Pin 17 = !FBLW;
Pin 18 = !ACK;
Pin 19 = !PIXL;
Pin 20 = HOSTL;
Pin 21 = !PIXAEN;
Pin 22 = !HOSTAEN;
Pin 23 = !CTLCS;

/* Address buffer enables */
HOSTAEN = !S2 & FBCS;
PIXAEN  = S2 & DISPEN;

/* Latch enables */
LATCHEN = !S1 & S0;
PIXL    = LATCHEN & S2;
HOSTL   = LATCHEN & !S2 & (UR # LR);

/* Frame buffer read/write enables */
FBUR = PIXAEN # (HOSTAEN & UR);
FBLR = PIXAEN # (HOSTAEN & LR);
FBUW = HOSTAEN & UW;
FBLW = HOSTAEN & LW;

/* Frame buffer / controller chip selects */
FBCS  = CS & !ID & !CTLADDR;
CTLCS = CS & !ID & CTLADDR;

ACK.d = LATCHEN & !S2 & (UR # LR # UW # LW);
ACK.ar = CS;
ACK.sp = ID;
