Name      68kglue2;
PartNo    1;
Date      2014-05-02;
Revision  00;
Designer  swallace;
Company   ;
Assembly  None;
Location  None;
Device    G22V10 ;

/*
                         22V10
                      _____ _____
             CLK/IN -| 1   U  24 |- VCC
                 IN -| 2      23 |- I/O	nAVEC
                 IN -| 3      22 |- I/O	nDUARTCS
              IN/PD -| 4      21 |- I/O	nATA1CS
                 IN -| 5      20 |- I/O	nATA0CS
  nPERIPHCS      IN -| 6      19 |- I/O	nETHCS
  nCPU_CYCLE     IN -| 7      18 |- I/O	nRTCCS
  A16            IN -| 8      17 |- I/O	
  A17            IN -| 9      16 |- I/O	
  A18            IN -| 10     15 |- I/O	
  A19            IN -| 11     14 |- I/O	
                GND -| 12     13 |- IN	
                     -------------
*/

/** Inputs **/
Pin 6          = !PERIPHCS;
Pin 7          = !CPU_CYCLE;
Pin [8..11]    = [A16..19];

/** Outputs **/

Pin 18         = !RTCCS;
Pin 19         = !ETHCS;
Pin 20         = !ATA0CS;
Pin 21         = !ATA1CS;
Pin 22         = !DUARTCS;
Pin 23         = !AVEC;


/*
    Interrupt autovector signal: always autovector.
*/
AVEC = CPU_CYCLE & ([A19..16]: 'h'0);

/*
    Peripheral chip selects
*/

Field Address = [A19..16];
Field Peripheral = [DUART, ATA0, ATA1, ETH, RTC];

Table Address => Peripheral
{
    [00000..0FFFF] => 'b'10000;    /* DUART                     */
    [10000..1FFFF] => 'b'01000;    /* ATA interface - channel 0 */
    [20000..2FFFF] => 'b'00100;    /* ATA interface - channel 1 */
    [30000..3FFFF] => 'b'00010;    /* Ethernet                  */
    [40000..4FFFF] => 'b'00001;    /* Real-time clock           */
}

DUARTCS = PERIPHCS & DUART;
ATA0CS = PERIPHCS & ATA0;
ATA1CS = PERIPHCS & ATA1;
ETHCS = PERIPHCS & ETH;
RTCCS = PERIPHCS & RTC;
