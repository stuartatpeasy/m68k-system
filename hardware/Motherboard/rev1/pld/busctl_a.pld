Name      busctl_a;
PartNo    1;
Date      2014-06-07;
Revision  00;
Designer  swallace;
Company   ;
Assembly  None;
Location  None;
Device    G22V10;

/*
Description
===========

PLD pin descriptions
====================

	[FC0..2]		1..3	[in]  CPU function codes
	[A18..23]		4..9	[in]  CPU address bus lines A23-A18
	nUDS			10	[in]  CPU nUDS
	nLDS			11	[in]  CPU nLDS
	nAS			13	[in]  CPU nAS
	WDOG			14	[in]  Watchdog timer timeout
	nEPCS			15	[out] Expansion slot / on-board peripheral chip select
	nEBERR			16	[in]  Expansion slots nEBERR
	nDTACK			17	[out] CPU nDTACK
	nTOK			18	[out] Transfer OK (no access violation detected)
	nBERR			19	[out] CPU nBERR
	nVPA			20	[out] CPU nVPA
	nEPACK			21	[in]  Transfer acknowledge from expansion slot / peripheral
	nMEMCS			22	[out] Memory module (RAM/ROM) chip select
	nEVPA			23	[in]  Signals nVPA asserted by any expansion slot

Notes
=====

This module must be compiled with Quine-McCluskey minimisation and de Morgan optimisation,
or the product terms will not fit into a 22V10 GAL.


Target device pinout
====================
                         22V10
                      _____ _____
  FC0        CLK/IN -| 1   U  24 |- VCC
  FC1            IN -| 2      23 |- I/O    nEVPA
  FC2            IN -| 3      22 |- I/O    nMEMCS
  A18         IN/PD -| 4      21 |- I/O    nEPACK
  A19            IN -| 5      20 |- I/O    nVPA
  A20            IN -| 6      19 |- I/O    nBERR
  A21            IN -| 7      18 |- I/O    nTOK
  A22            IN -| 8      17 |- I/O    nDTACK
  A23            IN -| 9      16 |- I/O    nEBERR
  nUDS           IN -| 10     15 |- I/O    nEPCS
  nLDS           IN -| 11     14 |- I/O    WDOG
                GND -| 12     13 |- IN     nAS
                      ----------- 
*/

/** Inputs **/

Pin [1..3] = [FC0..2];
Pin [4..9] = [A18..23];
/*
Pin 10 = !UDS;
Pin 11 = !LDS;
*/
Pin 13 = !AS;
Pin 14 = WDOG;
Pin 16 = !EBERR;
Pin 21 = !EPACK;
Pin 23 = !EVPA;

/** Outputs **/

Pin 15 = !EPCS;
Pin 17 = !DTACK;
Pin 18 = !TOK;
Pin 19 = !BERR;
Pin 20 = !VPA;
Pin 22 = !MEMCS;

/*
	Definitions
*/

/* Decode function code inputs to address space indicators */
Table [FC2..0] => [USERSPACE, CPUSPACE]
{
	'b'000 => 'b'00;	/* (undefined, reserved) */
	'b'001 => 'b'10;	/* User data */
	'b'010 => 'b'10;	/* User program */
	'b'011 => 'b'00;	/* (undefined, reserved) */
	'b'100 => 'b'00;	/* (undefined, reserved) */
	'b'101 => 'b'00;	/* Supervisor data */
	'b'110 => 'b'00;	/* Supervisor program */
	'b'111 => 'b'01;	/* CPU space */
}

Table [A23..A18] => [SUPERRAM, RAM, EXP, OBPERIPH, ROM]
{
	[000000..03FFFF] => 'b'10000;
	[040000..7FFFFF] => 'b'01000;
	[800000..9FFFFF] => 'b'00000;	/* nothing lives here */
	[A00000..DFFFFF] => 'b'00100;
	[E00000..EFFFFF] => 'b'00010;
	[F00000..FFFFFF] => 'b'00001;
}

/* Decide whether a cycle in progress is in CPU space or "standard" (user or supervisor) space */
CPUCYCLE = CPUSPACE & AS;

/* User cycle indicator */
USERCYCLE = USERSPACE & AS;

/* Detect error conditions */
RAMERR = USERCYCLE & SUPERRAM;
OBPERIPHERR = USERCYCLE & OBPERIPH;
ROMERR = USERCYCLE & ROM;

ERR = RAMERR # OBPERIPHERR # ROMERR # EBERR;

/* Detect CPU-space acknowledgement cycles */
BKPTACK = CPUCYCLE & !A19 & !A18;
IACK = CPUCYCLE & A19 & A18;

/* Generate bus error: transfer error or watchdog timeout */
BERR = (AS & ERR) # WDOG;

/* Transfer OK - no access violation detected */
TOK = AS & !BERR;

/* Memory chip select */
MEMCS = TOK & (SUPERRAM # RAM # ROM);

/* Expansion slots / on-board peripheral chip select */
EPCS = TOK & (OBPERIPH # EXP);

/* DTACK and VPA - transfer acknowledgements */
VPA = AS & (IACK # EVPA);
DTACK = BKPTACK # (TOK & (EPACK # MEMCS));
