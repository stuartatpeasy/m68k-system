Name      busctl_c;
PartNo    1;
Date      2014-06-09;
Revision  00;
Designer  swallace;
Company   ;
Assembly  None;
Location  None;
Device    G22V10;

/*
Description
===========

PLD pin descriptions
====================

	[A22..20]		[in]  CPU address bus lines A22-A20
	[A17..A16]		[in]  CPU address bus lines A17-A16
	nDUARTACK		[in]  DUART transfer acknowledge
	ATAIORDY		[in]  ATA transfer acknowledge
	nE[3..0]ACK		[in]  Expansion slot [3..0] transfer acknowledge
	nEPCS			[in]  Expansion slots / on-board peripheral chip select
	nEPACK			[out] Expansion slot or on-board peripheral transfer acknowledge
	nE[3..0]CS		[out] Expansion slot [3..0] chip select
	nATACS			[out] ATA chip select
	nATA[0..1]CS		[out] ATA channel [0..1] chip select
	nRTCCS			[out] RTC chip select
	nDUARTCS		[out] DUART chip select

Notes
=====


Target device pinout
====================
                         22V10
                      _____ _____
  A22        CLK/IN -| 1   U  24 |- VCC
  A21            IN -| 2      23 |- I/O    nDUARTCS
  A20            IN -| 3      22 |- I/O    nRTCCS
  A17         IN/PD -| 4      21 |- I/O    nATA0CS
  A16            IN -| 5      20 |- I/O    nATA1CS
  nDUARTACK      IN -| 6      19 |- I/O    nATACS
  ATAIORDY       IN -| 7      18 |- I/O    nE0CS
  nE0ACK         IN -| 8      17 |- I/O    nE1CS
  nE1ACK         IN -| 9      16 |- I/O    nE2CS
  nE2ACK         IN -| 10     15 |- I/O    nE3CS
  nE3ACK         IN -| 11     14 |- I/O    nEPACK
                GND -| 12     13 |- IN     nEPCS
                      ----------- 
*/

/** Inputs **/

Pin [1..3]   = [A22..20];
Pin [4..5]   = [A17..16];
Pin 6        = !DUARTACK;
Pin 7        = ATAIORDY;         
Pin 8        = !E0ACK;
Pin 9        = !E1ACK;
Pin 10       = !E2ACK;
Pin 11       = !E3ACK;
Pin 13       = !EPCS;

/** Outputs **/

Pin 14       = !EPACK;
Pin 15       = !E3CS;
Pin 16       = !E2CS;
Pin 17       = !E1CS;
Pin 18       = !E0CS;
Pin 19       = !ATACS;
Pin 20       = !ATA1CS;
Pin 21       = !ATA0CS;
Pin 22       = !RTCCS;
Pin 23       = !DUARTCS;

/*
	Definitions
*/

/* Expansion slot chip selects */
E0CS = EPCS & (!A22 &  A21 & !A20);	/* Slot 0 chip select */
E1CS = EPCS & (!A22 &  A21 &  A20);	/* Slot 1 chip select */
E2CS = EPCS & ( A22 & !A21 & !A20);	/* Slot 2 chip select */
E3CS = EPCS & ( A22 & !A21 &  A20);	/* Slot 3 chip select */

/* On-board peripheral chip selects */
PCS = EPCS & A22 & A21 & !A20;		/* peripherals are at 0xExxxxx */

DUARTCS = PCS & (!A17 & !A16);	/* DUART chip select */
RTCCS   = PCS & (!A17 &  A16);	/* RTC chip select */
ATA0CS  = PCS & ( A17 & !A16);	/* ATA channel 0 chip select */
ATA1CS  = PCS & ( A17 &  A16);	/* ATA channel 1 chip select */

ATACS = ATA0CS # ATA1CS;

/* Expansion slot transfer acknowledge */
EACK = (E3ACK & E3CS)
         # (E2ACK & E2CS)
         # (E1ACK & E1CS)
         # (E0ACK & E0CS);

/* On-board peripheral transfer acknowledge */
PACK = (ATACS & ATAIORDY)
            # (DUARTCS & DUARTACK)
            # RTCCS;

EPACK = EACK # PACK;
